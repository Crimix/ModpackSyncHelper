plugins {
    id 'java'
    id 'net.saliman.properties' version '1.5.1'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'io.github.crimix.replace-placeholders' version '2.0'
}

group app_group
version = version_major + '.' + version_minor
archivesBaseName = "ModpackSynHelper-" + modpack_name

repositories {
    mavenCentral()
}

configurations {
    shadowImplementation
    implementation.extendsFrom(shadowImplementation)
}

dependencies {
    shadowImplementation 'com.google.code.gson:gson:2.8.7'
    shadowImplementation 'org.jsoup:jsoup:1.14.1'
    shadowImplementation 'commons-io:commons-io:2.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

replaceResourcePlaceholders {
    enabled true
    filesToExpand "app.properties"
    extraProperties "version", "modpack_name", "modpack_sync_json_url"
}

replaceSourcePlaceholders {
    enabled true
    filesToExpand '**/CurseHelper.java'
    extraProperties "api_key"
}

jar {
    manifest {
        attributes 'Main-Class': 'com.black_dog20.modpacksynchelper.Main'
    }
}

shadowJar {
    configurations = [project.configurations.shadowImplementation]
    relocate 'com.google.gson', 'com.black_dog20.modpacksynchelper.shadow.gson'
    relocate 'org.jsoup', 'com.black_dog20.modpacksynchelper.shadow.jsoup'
    minimize()
    classifier '' // Replace the default jar
}

tasks.build.dependsOn shadowJar
jar.finalizedBy('shadowJar')

test {
    useJUnitPlatform()
}